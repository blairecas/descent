	.title	DESPPU
	.enabl	LC,AMA	; local labels, absolute addr mode
	.list	MEB	; enable macro-extensions binary listing
	.asect
.=0	; from address 0 (!)

; 32-chars symbols length, allow underscore
; macro11.exe -ysl 32 -yus -m ..\..\macro11\sysmac.sml -l _descnt_ppu.lst _descnt_ppu.mac
; custom linker (!) macroses can break it

@include inc_common.mac
@include inc_ppu_header.mac

; ///////////////////////////////////////////////////////////
; // PPU INIT
; ///////////////////////////////////////////////////////////

PPUMain:
	; stack is 540..1000
	mov	#1000, SP
	; clear upper RAM
	call	CLEAUP
	; wait for CPU asking to start phase 3
	mov	#PPUState/2, @#177010
	cmp     #30, @#177014
	bne	.-14

	clr	@#177026		; allow write to all planes (for font output)
	bic	#107, @#177066		; turn off interrupt on CPU-PPU channels	
	; timers
	bic	#^B0000001000000000, @#177054	; disable vsync in PPU
	bis	#^B0000000100000000, @#177054	; enable vsync in CPU
	call	PPTINI			; init pp timer
	bic	#200, @#177716		; turn off sound generator
	; set interrupts
	mov	#INTKeyboard,  @#300	; keyboard interrupt
	bis	#100, @#177700		; allow keyboard interrupts	
	mtps	#0			; allow interrupts

        ; acknowledge start phase 3
	mov	#<PPUState/2>, @#177010	
	mov	#03, @#177014

; //////////////////////////////////////////////////////////////////////////////
; // MAIN PPU CYCLE
; //////////////////////////////////////////////////////////////////////////////

MainCycle:
	call	MusPro
	call	CmdPro
	br	MainCycle

CmdAdr:	.word	0			; command addr/2 in CPU

; process commands from CPU
CmdPro:	mov	#CmdBuffer, @#177010
	tst	@#177014
	bne	10$
	inc	WAIPP0			; 'empty' cycles counter
	return	
10$:	mov	#CmdBuffer+1, CmdAdr	; start of commands address
20$:	cmp	CmdAdr, #CmdBuffer	; out of range (overflow)?
	blos	CmdError
	mov	#177010, R4
	mov	#177014, R5
	; get command
	mov	CmdAdr, (R4)
	mov	(R5), R0
	beq	90$			; end of commands?
	asl	R0			; set procedure addr
	mov	CommandsTable(R0), 00$+2
	; get parameters (R0..R4) and run
	inc	(R4)
	mov	(R5), R0
	inc	(R4)
	mov	(R5), R1
	inc	(R4)
	mov	(R5), R2
	inc	(R4)
	mov	(R5), R3
	inc	(R4)
	mov	(R5), R5
	inc	(R4)
	mov	(R4), CmdAdr
	mov	R5, R4
00$:	call	@#000000		; this addr modified earlier
	br	20$			; next command
90$:	; end of commands
	mov	#CmdBuffer, @#177010	; clear 'ready' word in vram
	clr	@#177014
	return
CmdError:				; TODO: error - do something here
	jmp	Error

; commands table
CommandsTable:
	.word	ERROR			; 00. can't be
	.word	CLEAUP			; 01. cleanup upper RAM
	.word	PALUPD			; 02. set palette, cursor, resolution, luminance (R0,R1 - palette, R2,R3 - cursor, resolution, lumi)
	.word	FXWAIT			; 03. write empty cycles count to CPU
	.word	REQRCN			; 04. write empty INTs count to CPU
	.word	SNDSTA			; 05. start sound
	.word	MUSSTA			; 06. start music
	.word	PrintStr		; 07. print single-height string
	.word	PrintMsg		; 08. print message in box
	.word	ClearVBuffer		; 09. clear buffer
	.word	DrawTile		; 10. draw 16x16 tile to x,y
	.word	DrawTileVaddr		; 11. draw 16x16 tile to vaddr
	.word	DrawTile8Vaddr		; 12. draw 8x8 tile to vaddr
	.word	BufTile			; 13. tile 16x16 to buffer
	.word	BufSprite		; 14. 16x16 sprite with mask to buffer
	.word	Buf2Scr16		; 15. buffer tile 16x16 to screen
	.word	Clr2Scr16		; 16. clear tile 16x16 on screen
	.word	FillScreen		; 17. fill screen plane 0 with R0
	.word	LinesFromR0		; 18. set lines vaddrs starting from R0
	.word	HideScreen		; 19. hides screen (8 lines of bottom is on center)
	.word	DebugString		; 20. show info on bottom of screen
	.word	FadeInScreen		; 21. screen fade in, R0=1..144.
	.word	FadeOutScreen		; 22. screen fade out, R0=1..144.
	.word	UpdItem			; 23. update item box with sprite
	.word	UnpackLevels		; 24. unpack levels data PPU vram -> CPU vram
	.word	DrawLifeBgr		; 25. draw player life bar background
	.word	DrawLifeCur		; 26. draw player life line
	.word	ClrScrBottom		; 27. clear last 12 lines of screen vmem

	
; //////////////////////////////////////////////////////////////////////////////
; // Procedures
; //////////////////////////////////////////////////////////////////////////////

; empty interrupt
RTIADR:	inc	(PC)+
RTICNT:	.word	0
	rti

; pp timer ready wait
PptIni:	clr	@#177710
	tst	@#177714
	tst	@#177710
	bne	PPTINI
	return

; 01: clear upper RAM
CleaUp:	mov	#END, R0
	mov	#100000-END/2, R2
	clr	(R0)+
	sob	R2, .-2
	return

; 16: fill screen plane 0 with R0
FillScreen:
	mov	#VADDR, R5
	mov	#SCRWID/2*288., R3
10$:	mov	R0, (R5)+
	sob	R3, 10$
	return
	
; clear last 12 lines of screen vmem
ClrScrBottom:
	call	SetVreg
	mov	#177014, R2
	mov	#SCRWID*288.+VADDR, (R4)
	mov	#SCRWID*12., R3
10$:	clr	(R5)
	clr	(R2)
	inc	(R4)
	sob	R3, 10$
	return

; 03: pallete, resolution, cursor, luminance change
PalUpd:	mov	#VLinesBegin, R5
	mov	R0, (R5)+
	mov	R1, (R5)+
	cmp	(R5)+, (R5)+
	mov	R2, (R5)+
	mov	R3, (R5)
	return

; 04: write empty cycles count to CPU
WAIPP0:	.word	0		; PPU wait cycles counter
FXWAIT:	mov	#<PpuWaitCount/2>, @#177010
	mov	WAIPP0, @#177014
	clr	WAIPP0
	return

; 05: request RTICNT to CPU
REQRCN:	mov	#<PpuRtiCount2/2>, @#177010
	mov	RTICNT, @#177014
	return


; //////////////////////////////////////////////////////////////////////////////
; // UTILS 
; //////////////////////////////////////////////////////////////////////////////

@include inc_ppu_utils.mac


; //////////////////////////////////////////////////////////////////////////////
; // sound and music
; //////////////////////////////////////////////////////////////////////////////

MUSCUR:	.word	0 
SNDADR:	.word	0

; music list addrs
MUSTAB:	.word	0, MUS001
; sound list addrs
SNDTAB:	.word	0, SND001, SND002, SND003

.radix 10

; notes (TODO: need correction a bit, some notes are sounding not that right)
NOTPAD:	.word	1000
	.word	956, 902, 851, 804, 758, 716, 676, 638, 602, 568, 536, 506
	.word	478, 451, 426, 402, 379, 358, 338, 319, 301, 284, 268, 253
	.word	239, 225, 213, 201, 190, 179, 169, 159, 150, 145, 134, 127
	.word	119, 113, 106, 100, 95,  89,  84,  80,  75,  73,  67,  63

; C             D             E      F             G             A             B
; C4-37  C#4-38 D4-39  D#4-40 E4-41  F4-42  F#4-43 G4-44  G#4-45 A4-46  A#4-47 B4-48
; C3-25  C#3-26 D3-27  D#3-28 E3-29  F3-30  F#3-31 G3-32  G#3-33 A3-34  A#3-35 B3-36
; C2-13  C#2-14 D2-15  D#2-16 E2-17  F2-18  F#2-19 G2-20  G#2-21 A2-22  A#2-23 B2-24
; C1-01  C#1-02 D1-03  D#1-04 E1-05  F1-06  F#1-07 G1-08  G#1-09 A1-10  A#1-11 B1-12

.radix 8

SndType:	.word	0		; sound type
SndTCnt:	.word	0		; total cycles count (use additions every cycle)
SndLCnt:	.word	0		; local cycles count
SndLDCnt:	.word	0		; local cycles addition
SndLLo:		.word	0		; low level length (fixed point, high byte used)
SndLDl:		.word	0		; low level length addition
SndLHi:		.word	0		; high level length
SndLDh:		.word	0		; high level length addition
SndIntLCnt:	.word	0


; start sound, R0 - sound #
SNDSTA:	mtps	#600			; stop interrupts just in case
	call	PPTINI			; init timer (stops previous sounds)
	clr	MUSCUR			; stop music
	asl	R0
	mov	SNDTAB(R0), R0		; sound addr
	beq	99$			; maybe just exit? (can be used to stop sound)
	call	SndSetVars
	mov	#SndIntLo, @#304	; set sound interrupt to low signal proc
	mov	#1, @#177712		; timer counter to minimum
	tst	@#177714
	tst	@#177714
	bis	#^B01000011, @#177710	; allow int, start, timer = 4mks, 250000 ticks per second
99$:	mtps	#0
	return

SndSetVars:
	mov	(R0)+, SndType		; type of sound effect
	beq	99$			; none - exit
	mov	(R0)+, SndTCnt		; total counter
	mov	(R0)+, SndLCnt		; local counter
	mov	(R0)+, SndLDCnt		; local counter addition
	mov	(R0)+, SndLLo		; low level duration
	mov	(R0)+, SndLDl		; low level duration addition
	mov	(R0)+, SndLHi		; high level duration
	mov	(R0)+, SndLDh		; high level duration addition
	mov	R0, SNDADR		; store next effect addr
	mov	SndLCnt, SndIntLCnt	; set local counter to interrupt procedure		
99$:	return	

; PP timer interrupts for sounds
SndIntLo:
	bic	#200, @#177716		; set 
	mov	#SndIntHi, @#304	; set next interrupt to high-level version
	; cycle without changes to low|high durations
	decb	SndIntLCnt+1		; (!) only h-byte is used
	bpl	90$
	; change durations of low|high
	add	SndLDl, SndLLo		; additions to low|high durations
	add	SndLDh, SndLHi		;
	add	SndLDCnt, SndLCnt	; addition to local cycles count
	mov	SndLCnt, SndIntLCnt	; restore local cycles count
	; end of effect?
	dec	SndTCnt
	bpl	90$
	; try next effect
	;mov	R0, -(SP)
	;mov	SNDADR, R0
	;call	SndSetVars
	;mov	(SP)+, R0
	;tst	SndType
	;bne	90$
	; no next effect - stop timer
	clr	@#177710
	tst	@#177714
	tst	@#177714
	rti
90$:	movb	SndLLo+1, @#92$+2	; only high-byte used
92$:	mov	#000000, @#177712
	tst	@#177714
	tst	@#177714
	rti
SndIntHi:
	bis	#200, @#177716
	mov	#SndIntLo, @#304
	movb	SndLHi+1, @#92$+2
92$:	mov	#000000, @#177712
	tst	@#177714
	tst	@#177714
	rti


; start music, R0 - music #
MUSSTA:	mtps	#600			; stop interrupts
	call	PPTINI			; init pp timer
	tst	R0			; 0 - stop music
	bne	10$
	clr	MUSCUR
	mtps	#0
	return
10$:	asl	R0
	mov	MUSTAB(R0), R0		; addr from music table
	movb	(R0), R1		; first note #
	asl	R1
	mov	NOTPAD(R1), @#177712	; initial counter
	mov	R0, MUSCUR		; first note addr
	tst	@#177714
	tst	@#177714
	mov	#MusIntLo, @#304	; set music interrupt
	bis	#^B01000001, @#177710	; allow int, start, timer = 2mks, 500000 ticks per second
	mtps	#0
	return

; mus processor
MUSPRO:	tst	MUSCUR			; current note addr
	bne	1$			; are we are playing?
	return				; no - return
1$:	mov	#<VSyncCount2/2>, @#177010
	cmp	@#177014, #1		; was #2 vsync?
	bhi	2$
	return
2$:	mov	#<VSyncCount2/2>, @#177010
	clr	@#177014
	; process new note
	mtps	#600			; stop interrupts
	inc	MUSCUR			; advance music note
	movb	@MUSCUR, R1		; note #
	beq	20$			; empty ID - stop playing
	bmi	10$			; empty note
	asl	R1
	mov	NOTPAD(R1), R1		; note delays initial
	mov	R1, MusCountHi+2
	mov	R1, MusCountLo+2
	mov	R1, @#177712		; set music interrupt timer
	tst	@#177714		; maybe better way here is  
	tst	@#177714		; TST @#177714, BNE .-4
	mov	#200, MusIntLo+2	; set #200 in bis/bic
	mov	#200, MusIntHi+2	; in case we cleared it
	br	99$
10$:	; stop ticking beeper
	clr	MusIntLo+2		; clear #200 in bis/bic
	clr	MusIntHi+2		; so it will not tick beeper
	br	99$
20$:	; stop playing
	call	PPTINI			; init PP timer
	clr	MUSCUR			; music is stopped
99$:	mtps	#0
	return

; timer interrupts (for tones)
MusIntLo:
	bic	#200, @#177716		; tick low level
	mov	#MusIntHi, @#304	; set next interrupt to high-level version
MusCountLo:
	mov	#000000, @#177712	; set new timer as duty changed
	add	#2, MusCountLo+2	; change duty (maybe add 2?)
	tst	@#177714		; TWO (!) times is important I think
	tst	@#177714
	rti
; same as above just for high signal level
MusIntHi:
	bis	#200, @#177716
	mov	#MusIntLo, @#304
MusCountHi:
	mov	#000000, @#177712
	sub	#2, MusCountHi+2	; do we need to test it for close to 0?
	tst	@#177714
	tst	@#177714
	rti		


; //////////////////////////////////////////////////////////////////////////////
; // KEYBOARD
; //////////////////////////////////////////////////////////////////////////////

; keyboard interrupt
INTKeyboard:
	mov	R0, -(SP)
	mov	@#177702, R0		; (!) read this register ONLY ONCE
	mov	R1, -(SP)
	mov	R2, -(SP)
	mov	R3, -(SP)
	mov	R4, -(SP)
	mov	R5, -(SP)
	mov	#177010, R4
	mov	#177014, R5
	mov	(R4), -(SP)		; save vaddr register 
	; now R0-keycode, (R4)-vaddr, (R5)-vdata
	mov	#KeyCurrent/2, (R4)
	mov	R0, (R5)
	bit	#^B10000000, R0		; key was released?
	bne	50$
	; 0 2 4 6 8 10 == dn,dn,up,up,dn,up ? -> combo is pressed
	; put pressed keys to buffer
	mov	#KeysBuffer, R1
	mov	#KeysBuffer+1, R2
	movb	(R2)+, (R1)+
	movb	(R2)+, (R1)+
	movb	(R2)+, (R1)+
	movb	(R2)+, (R1)+
	movb	(R2)+, (R1)+
	movb	R0, (R1)+
	; check for key buffer combo ^_^
	mov	#6., R3
2$:	cmpb	(R2)+, -(R1)
	bne	4$
	sob	R3, 2$	
	mov	#KeyCombo/2, (R4)	; set 'combo is pressed'
	inc	(R5)
4$:	; -----------------
	; == key pressed ==
	mov	#KeyAny/2, (R4)		; 'anykey' is also pressed
	inc	(R5)
	mov	#KEYSDN, R1
10$:	tst	(R1)
	beq	90$
	cmp	R0, (R1)+
	beq	20$
	add	#4, R1
	br	10$
20$:	; got our key
	mov	(R1)+, (R4)		; set CPU addr
	inc	(R5)			; must be mov #1,(R5) but its ok with inc
	mov	(R1)+, R2
	beq	10$	
	mov	R2, (R4)
	clr	(R5)
	br	10$
50$:	; == key released - R0 = ^B1000xxxx
	bic	#^B1111111111110000, R0
	asl	R0
	mov	KEYSUP+2(R0), R1	; finish addr
	mov	KEYSUP(R0), R0		; start addr
60$:	cmp	R0, R1			; finished?
	bhis	90$
	mov	(R0)+, (R4)		; set CPU addr
	clr	(R5)			; and clear key
	br	60$
	;
90$:	mov	(SP)+, (R4)
	mov	(SP)+, R5
	mov	(SP)+, R4
	mov	(SP)+, R3
	mov	(SP)+, R2
	mov	(SP)+, R1
	mov	(SP)+, R0
	rti

; keys release - clear addrs
KEYSUP:	.word	KUP0000, KUP0001, KUP0010, KUP0011, KUP0100, KUP0101, KUP0110, KUP0111
	.word	KUP1000, KUP1001, KUP1010, KUP1011, KUP1100, KUP1101, KUP1110, KUP1111
	.word	KUP7777
KUP0000:
KUP0001:
KUP0010:
KUP0011:
KUP0100:	.word	KeyStop/2
KUP0101:	.word	KeyShift/2
KUP0110:
KUP0111:
KUP1000:	.word	Key1/2, KeyK1/2
KUP1001:	.word	Key2/2, KeyK2/2
KUP1010:	.word	Key3/2, KeyUst/2
KUP1011:	.word	Key4/2, KeySpace/2, KeyRight/2
KUP1100:	.word	Key5/2, KeyDown/2, KeyUp/2
KUP1101:	.word	Key6/2
KUP1110:	.word	Key7/2, KeyLeft/2
KUP1111:	.word	Key8/2
KUP7777:

; keys press - set 1st addr, clear second
KEYSDN:	.word	^B00000100, KeyStop/2,		0
	.word	^B01101010, KeyUst/2,		0
	.word	^B00011000, Key1/2,		0
	.word	^B00011001, Key2/2,		0
	.word	^B00011010, Key3/2,		0
	.word	^B00001011, Key4/2,		0
	.word	^B00011100, Key5/2,		0
	.word	^B00011101, Key6/2,		0
	.word	^B00001110, Key7/2,		0
	.word	^B00001111, Key8/2,		0
	.word	^B01001011, KeySpace/2,		0
	.word	^B01011100, KeyDown/2,		KeyUp/2
	.word	^B01101100, KeyUp/2,		KeyDown/2
	.word	^B01001110, KeyLeft/2, 		KeyRight/2
	.word	^B01011011, KeyRight/2,		KeyLeft/2
	.word	^B00001000, KeyK1/2,		0
	.word	^B00001001, KeyK2/2,		0
	.word	^B01000101, KeyShift/2,		0
	.word	0
	
KeysBuffer:	.BLKB	6				; last 6 keys
		.BYTE	154, 134, 154, 154, 134, 134	; dn,dn,up,up,dn,up written backwards


; //////////////////////////////////////////////////////////////////////////////
; // PPU ERROR - TODO ?? DEBUG INFO ??
; //////////////////////////////////////////////////////////////////////////////

Error:	mtps	#600			; halt mode
	br	.

; //////////////////////////////////////////////////////////////////////////////
; // DEBUG SCREEN 
; //////////////////////////////////////////////////////////////////////////////

; word to string in decimal xxxxx, R0-word, R5-str addr
DecToStr:
	mov	#5, R3
	add	R3, R5
1$:	mov	R0, R1
	clr	R0
	div	#10., R0
	add	#'0, R1
	movb	R1, -(R5)
	sob	R3, 1$
	return


; put some information at the bottom of screen
DebugString:	
	inc	(PC)+			; set 'already updated'
	.word	177777			; if string is already updated - then skip
	bne	99$
	; R0 = free CPU bytes
	mov	#TxtDebug01+15., R5
	call	DecToStr
	; R4 = free VRAM12 bytes
	mov	R4, R0
	mov	#TxtDebug03+11., R5
	call	DecToStr
	; put free PPU bytes
	mov	#100000-END, R0
	mov	#TxtDebug01+27., R5
	call	DecToStr
	; put free VRAM0 bytes
	mov	#CmdBuffer-ENDVram0, R0
	mov	#TxtDebug03+23., R5
	call	DecToStr
	; check for real 1801VM2
	clr	R1
	mov	(PC), R0
	inc	R1
	br	10$
10$:	cmp	R1, #1
	beq	99$
	mov	#TxtDebug02, R1		; copy 9 bytes of text
	mov	#TxtDebug01+35., R2
	mov	#9., R3
	movb	(R1)+, (R2)+
	sob	R3, .-2
99$:	; out free CPU/PPU RAM string
	mov	#5., R1
	mov	#270., R2
	mov	#TxtDebug01, R0
	call	PrintStr
	; out free video RAM string
	mov	#8., R1
	mov	#279., R2
	mov	#TxtDebug03, R0
	call	PrintStr
	return


; //////////////////////////////////////////////////////////////////////////////
; // LZ decompressing VRAM PPU -> VRAM CPU
; //////////////////////////////////////////////////////////////////////////////
; lzsa3 decompressing routines by manwe & ivanq
; https://github.com/imachug/lzsa3

; Unpacking LZ-compressed levels from PPU vram to CPU vram
; R0 - CPU vram GlobalLevel, GlobalObj addr relative to 200000
UnpackLevels:
	mov	R0, -(SP)		; save GlobalLevel addr in CPU vram
	; unpack GlobalObj, they are [byte]
	mov	R1, PutA12
	mov	#GlobalObj, GetA00
	call	Unpa01
	; unpack GlobalLevel to [byte]
	mov	(SP), PutA12
	mov	#GlobalLevel, GetA00
	call	Unpa01
	; now unpacked level is in [byte] but we need [word]
	mov	(SP)+, R0
	mov	R0, R1			; set source addr in the end of unpacked bytes
	add	#CGL_DX*CGL_DY-1, R1	; source is relative to 200000
	mov	R0, R2			; set destination addr
	add	#CGL_DX*CGL_DY*2-2, R2	; in the end of GlobalLevel
	sec				; convert R2 to vaddr in CPU
	ror	R2			; destination is NOT relative to 200000
	mov	#CGL_DX*CGL_DY-1, R3	; relocate (LEN-1) bytes because first byte not needed to relocate
	mov	#177010, R4
	mov	#177014, R5
10$:	; get byte from relative [R1] -> R0
	mov	R1, R0
	sec
	ror	R0
	bcs	20$
	mov	R0, (R4)
	movb	(R5), R0
	br	30$
20$:	mov	R0, (R4)
	movb	@#177015, R0
30$:	; put R0 to -> [byte,?]
	mov	R2, (R4)
	mov	R0, (R5)
	dec	R2
	dec	R1
	sob	R3, 10$
	return

; get byte -> R1 from PPU vaddr and advance it
GetB00:	mov	(PC)+, R1
GetA00:	.word	0
	inc	GetA00
	mov	R1, @#177010
	movb	@#177012, R1
	return

; put R1 -> byte to dest CPU vaddr and advance it
; invalidates R2
PutB12:	mov	(PC)+, R2
PutA12:	.word	0			; CPU vram addr rel to 200000
	inc	PutA12
	sec
	ror	R2
	bcs	10$
	mov	R2, @#177010
	movb	R1, @#177014
	return
10$:	mov	R2, @#177010
	movb	R1, @#177015
	return

; get CPU vram [R1] byte -> R1
;GetB12:	mov	(PC)+, R1
;GetA12:	.word	0			; vram addr rel to 200000
;	inc	GetA12
GetR12:	sec
	ror	R1
	bcs	10$
	mov	R1, @#177010
	movb	@#177014, R1
	return
10$:	mov	R1, @#177010
	movb	@#177015, R1
	return

; VRAM -> VRAM
Unpa01:	CLR	R5		; no nibbles sign
10$:	call	GetB00		; +
	movb	R1, R3		; + movb (R1)+, R3
	MOV	R3, R0
	BIC	#177774, R0	; get 2 bits
	BEQ	25$
	CMP	R0, #3		; literals length
	BNE	20$
	CALL	65$
20$:	call	GetB00		; +
	call	PutB12		; + movb (R1)+, (R2)+
	SOB	R0, 20$
25$:	MOV	R3, -(SP)
	ROLB	R3		; get 2 bits
	ROL	R0
	ROLB	R3
	ROL	R0
	ASL	R0
	ADD	R0, PC		; run subroutine
	BR	55$
	BR	45$
	BR	50$
	CALL	60$		; get nibble in R0
	ROLB	R3
	ROL	R0
	INC	R0
30$:	MOV	R0, R4		; save offset for future
35$:	MOV	(SP)+, R0
	ASR	R0
	ASR	R0
	BIC	#177770, R0	; get 3 bits
	CMP	R0, #7
	BNE	40$
	CALL	65$
	TSTB	R0		; match length
	BEQ	99$
40$:	mov	PutA12, R1	; + copy: dst - R4 -> dst, original was:
	sub	R4, R1		; + mov r2, r3
	call	GetR12		; + sub r4, r3
	call	PutB12		; + movb (r3)+,(r2)+
	inc	R0		; + inc r0
1$:	mov	PutA12, R1	; + 1$: movb (r3)+,(r2)+
	sub	R4, R1		; + sob r0, 1$
	call	GetR12		; +
	call	PutB12		; +
	sob	R0, 1$		; +
	BR	10$
45$:	CLR	R0
	call	GetB00		; +
	bisb	R1, R0		; + bisb (R1)+, R0
	ROLB	R3
	ROL	R0
	INC	R0
	BR	30$
50$:	CALL	60$ 		; get nibble in R0
	ROLB	R3
	ROL	R0
	SWAB	R0
	call	GetB00		; +
	bisb	R1, R0		; + bisb (R1)+, R0
	ADD	#513., R0
	BR	30$
55$:	ROLB	R3
	BCS	35$
	call	GetB00		; +
	bisb	R1, R0		; + bisb (R1)+, R0
	SWAB	R0
	call	GetB00		; +
	bisb	R1, R0		; + bisb (R1)+, R0
	BR	30$
60$:	COM	R5
	BMI	2$
	MOV	R5, R0
	CLR	R5
	BR	3$
2$:	call	GetB00		; +
	bicb	R1, R5		; + bicb (R1)+, R5
	MOV	R5, R0
	ASR	R0
	ASR	R0
	ASR	R0
	ASR	R0
3$:	BIC	#177760, R0	; leave 4 low bits
	RETURN
65$:	MOV	R0, -(SP)	; save original value
	CALL	60$ 		; get nibble in R0
	BNE	75$
	call	GetB00		; +
	bisb	R1, R0		; + bisb (R1)+, R0
	BNE	70$
	; unnecessary for short files
	call	GetB00		; +
	bisb	R1, R0		; + bisb (R1)+, R0
	SWAB	R0
	call	GetB00		; +
	bisb	R1, R0		; + bisb (R1)+, R0
	inc	SP		; skip saved R0
	inc	SP
	return
70$:	ADD	#15., R0
75$:	DEC	R0
	ADD	(SP)+, R0	; add original value
99$:	return



; //////////////////////////////////////////////////////////////////////////////
; // PPU DATA SECTION //////////////////////////////////////////////////////////
; //////////////////////////////////////////////////////////////////////////////

; // SOUND & MUSIC /////////////////////////////////////////////////////////////

.radix 10

; C             D             E      F             G             A             B
; C4-37 C#4-38  D4-39 D#4-40  E4-41  F4-42 F#4-43  G4-44 G#4-45  A4-46 A#4-47  B4-48
; C3-25 C#3-26  D3-27 D#3-28  E3-29  F3-30 F#3-31  G3-32 G#3-33  A3-34 A#3-35  B3-36
; C2-13 C#2-14  D2-15 D#2-16  E2-17  F2-18 F#2-19  G2-20 G#2-21  A2-22 A#2-23  B2-24
; C1-01 C#1-02  D1-03 D#1-04  E1-05  F1-06 F#1-07  G1-08 G#1-09  A1-10 A#1-11  B1-12

; music #1
MUS001:
	.byte	13, 16, 20
	.byte	13, 16, 20

	.byte	13, 16, 20
	.byte	13, 16, 20

	.byte	08, 13, 16
	.byte	08, 13, -1

	.byte	10, 13, -1
	.byte	12, 15, -1
	
	.byte	13, 16, -1
	.byte	12, 15, -1
	
	.byte	13, 16, -1
	.byte	15, 12, -1
	
	.byte	16, 13, -1
	.byte	15, 12, -1
	
	.byte	16, 13, -1
	.byte	18, 09, -1

	.byte	12, 15, 20
	.byte	12, 15, 20
	
	.byte	12, 15, 20
	.byte	12, 15, 20
	
	.byte	08, 12, 15
	.byte	08, 12, -1

	.byte	10, 13, -1
	.byte	12, 15, -1
	
	.byte	13, 16, -1
	.byte	12, 15, -1
	
	.byte	13, 16, -1
	.byte	15, 18, -1
	
	.byte	16, 08, -1
	.byte	15, 09, -1
	
	.byte	16, 08, -1
	.byte	18, 09, -1

	.byte	13, 20, -1
	.byte	13, 20, -1

	.byte	25, 13, -1
	.byte	25, 13, -1

	.byte	25, 15, -1
	.byte	25, 15, -1

	.byte	25, 17, -1
	.byte	25, 17, -1

	.byte	25, 18, -1
	.byte	25, 18, -1

	.byte	18, 13, -1
	.byte	18, 13, -1

	.byte	18, 11, -1
	.byte	18, 11, -1

	.byte	18, 09, -1
	.byte	18, 09, -1

	.byte	16, 08, -1
	.byte	16, 08, -1

	.byte	20, 08, -1
	.byte	20, 08, -1

	.byte	20, 13, -1
	.byte	20, 13, -1

	.byte	20, 16, -1
	.byte	20, 16, -1

	.byte	22, 14, -1
	.byte	22, 14, -1

	.byte	15, 12, -1
	.byte	15, 12, -1

	.byte	15, 11, -1
	.byte	15, 11, -1

	.byte	13, 10, -1
	.byte	13, 10, -1

	.byte	12, 15, 20
	.byte	12, 15, 20

	.byte	0
	.even


SND_TONE = 1

; TYPE: SND_TONE
; ARGS: TCNT  = total repeats count (use additions every cycle)
;       LCNT  = local repeats count
;	LDCNT = local repeats addition
;       Lo = low signal [______/'''''''] length
;       Dl = addition to Lo
;	Hi = high signal length
;       Dh = addition to Hi
; jump
SND001:	.word	SND_TONE, 20, 01*256, 0, 250*256, -9*256,  250*256, -9*256
	.word	0
; spadeing
SND002:	.word	SND_TONE, 05, 01*256, 0, 20*256, +02*256,  200*256, -30*256
	.word	0
; add item
SND003:	.word	SND_TONE, 04, 25*256, -4*256, 80*256, 40*256, 70*256, 40*256
	.word	0


.radix 8


; // SPRITES ////////////////////////////////////////////////////////////////////

@include inc_ppu_sprites.mac	

TxtTable:	.word	TXT_Menu00, TXT_Menu01, TXT_Menu02, TXT_Menu03, TXT_Menu04, TXT_MENU05, TXT_Menu06, TXT_Test1, TXT_Pre_Intro, TXT_Msg1
		.word	TXT_Msg2, TXT_Game_Over


TxtDebug01:	.asciz	<1>" Свободно ЦП:"<2>"00000"<1>", ПП:"<2>"00000"<1>", эмуляция"
TxtDebug02:	.asciz	<3>"1801вм2"
TxtDebug03:	.asciz	<1>"Видео 12:"<2>"00000"<1>", В0:"<2>"00000"

		.even

VBufferSize = FieldDX*32.*FieldDY
VBuffer0:	.blkb	VBufferSize

END:
; RAM will be cleared after this

; //////////////////////////////////////////////////////////////////////////////
; // VRAM 
; //////////////////////////////////////////////////////////////////////////////

.=VDATA

; compressed levels
@include inc_levels_lz.mac

; game text messages are also in VRAM
TXT_Menu00:	.asciz	<7>"<>'`"<5>" - "<6>"Движение"
TXT_Menu01:	.asciz	<7>"1..8"<5>" - "<6>"Предмет"
TXT_Menu02:	.asciz	<7>" РЕГ"<5>" - "<6>"Использовать"
TXT_Menu03:	.asciz	<7>"  К1"<5>" - "<6>"Палитра "<4>"R"<2>"G"<1>"B"<6>"/"<2>"G"<4>"R"<1>"B"
TXT_Menu04:	.asciz	<7>" УСТ"<5>" - "<6>"Пауза"
TXT_Menu05:	.asciz	<7>"СТОП"<5>" - "<6>"Сброс игры"
TXT_Menu06:	.asciz	<6>" НАЖМИТЕ "<7>"ПРОБЕЛ"

TXT_Test1:	.asciz	<2>"Тест"

TXT_Pre_Intro:	.asciz	<7>"═╡ Ночная смена ╞═"
TXT_Game_Over:	.asciz	<7>"Тут и сказочке конец"

TXT_Msg1:	.asciz	<2>"Твоя смена. Лопата в шахте"<12><2>"Накопанное сложишь в этот"<12><2>"большой ящик"
TXT_Msg2:	.asciz	<6>"Тут темно и опасно"<12><6>"Нужен какой-то"<12><6>"Источник света"

ENDVram0:
